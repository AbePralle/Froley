module CSQ

class Cmd
  PROPERTIES
    t : Token

  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 0 )

    method ends_with_semicolon->Logical
      return true

    method write( builder:StringBuilder )
      builder.print( this->String )
endClass

class CmdList : Cmd
  PROPERTIES
    list : Cmd[]

  METHODS
    method init( t )
      list = Cmd[](5)

    method init( t, args:CmdInitArgs )
      list = Cmd[]( args.count )
      list.add( forEach in args )

    method count->Int32
      return list.count

    method first->Cmd
      return list.first

    method get( index:Int32 )->Cmd
      return list[ index ]

    method last->Cmd
      return list.last
endClass

class CmdStatements : CmdList
  METHODS
    method to->String
      return list.join( "\n" )

    method write( builder:StringBuilder )
      forEach (item at index in this)
        if (index > 0) builder.println
        item.write( builder )
        if (item.ends_with_semicolon) builder.print( ";" )
      endForEach
endClass

class CmdArgs : CmdList
  METHODS
    method to->String
      return "($)" (list.join(","))

    method write( builder:StringBuilder )
      forEach (item at index in this)
        if (index > 0) builder.print( ',' )
        item.write( builder )
      endForEach
endClass

class CmdUnary( t, operand:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 1 )
      operand = args.first

    method op->String
      throw UnsupportedOperationError()

    method to->String
      return "($$)" (op,operand)
endClass

class CmdPostUnary : CmdUnary
  METHODS
    method to->String
      return "($$)" (operand,op)
endClass

class CmdBinary( t, left:Cmd, right:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 2 )
      left = args.first
      right = args.last

    method op->String
      throw UnsupportedOperationError()

    method to->String
      return "($ $ $)" (left,op,right)
endClass


class CmdIf( t, condition:Cmd, body:Cmd, else_body:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 3 )
      condition = args[ 0 ]
      body = args[ 1 ]
      else_body = args[ 2 ]

    method ends_with_semicolon->Logical
      return false

    method to->String
      return "if($,$,$)"(condition,body,else_body)

    method write( builder:StringBuilder )
      builder.print( "if (" )
      condition.write( builder )
      builder.println( ")" )
      builder.println( "{" )
      builder.indent += 2
      body.write( builder )
      builder.println
      builder.indent -= 2
      builder.println( "}" )
      if (else_body)
        builder.println( "else" )
        builder.println( "{" )
        builder.indent += 2
        else_body.write( builder )
        builder.println
        builder.indent -= 2
        builder.println( "}" )
      endIf
endClass

class CmdFor( t, initializer:Cmd, condition:Cmd, modifier:Cmd, body:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 4 )
      initializer = args[ 0 ]
      condition = args[ 1 ]
      modifier = args[ 2 ]
      body = args[ 3 ]

    method ends_with_semicolon->Logical
      return false

    method to->String
      return "for($,$,$,$)"(initializer,condition,modifier,body)

    method write( builder:StringBuilder )
      builder.print( "for (" )
      initializer.write( builder )
      builder.print( ";" )
      condition.write( builder )
      builder.print( ";" )
      modifier.write( builder )
      builder.println( ")" )
      builder.println( "{" )
      builder.indent += 2
      body.write( builder )
      builder.println
      builder.indent -= 2
      builder.println( "}" )
endClass

class CmdWhile( t, condition:Cmd, body:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 2 )
      condition = args[ 0 ]
      body = args[ 1 ]

    method to->String
      return "while($,$)"(condition,body)
endClass

class CmdName( t, value:String ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 0 )
      value = t.content

    method to->String
      return value
endClass

class CmdLocalDeclaration( t, type:Cmd, name:Cmd, initial_value:Cmd ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 3 )
      type = args[ 0 ]
      name = args[ 1 ]
      initial_value = args[ 2 ]

    method to->String
      return "localDeclaration($,$,$)"(type,name,initial_value)

    method write( builder:StringBuilder )
      builder.print( type ).print( " " ).print( name )
      if (initial_value) builder.print( " = " ).print( initial_value )
endClass

class CmdAssign : CmdBinary
  METHODS
    method op->String
      return "="
endClass

class CmdAddAndAssign : CmdBinary
  METHODS
    method op->String
      return "+="
endClass

class CmdSubtractAndAssign : CmdBinary
  METHODS
    method op->String
      return "-="
endClass

class CmdMultiplyAndAssign : CmdBinary
  METHODS
    method op->String
      return "*="
endClass

class CmdDivideAndAssign : CmdBinary
  METHODS
    method op->String
      return "/="
endClass

class CmdModAndAssign : CmdBinary
  METHODS
    method op->String
      return "%="
endClass

class CmdAndAndAssign : CmdBinary
  METHODS
    method op->String
      return "&="
endClass

class CmdOrAndAssign : CmdBinary
  METHODS
    method op->String
      return "|="
endClass

class CmdCompareEQ : CmdBinary
  METHODS
    method op->String
      return "=="
endClass

class CmdCompareNE : CmdBinary
  METHODS
    method op->String
      return "!="
endClass

class CmdCompareLT : CmdBinary
  METHODS
    method op->String
      return "<"
endClass

class CmdCompareGT : CmdBinary
  METHODS
    method op->String
      return ">"
endClass

class CmdCompareLE : CmdBinary
  METHODS
    method op->String
      return "<="
endClass

class CmdCompareGE : CmdBinary
  METHODS
    method op->String
      return ">="
endClass

class CmdAdd : CmdBinary
  METHODS
    method op->String
      return "+"
endClass

class CmdSubtract : CmdBinary
  METHODS
    method op->String
      return "-"
endClass

class CmdMultiply : CmdBinary
  METHODS
    method op->String
      return "*"
endClass

class CmdDivide : CmdBinary
  METHODS
    method op->String
      return "/"
endClass

class CmdNegate : CmdUnary
  METHODS
    method op->String
      return "-"
endClass

class CmdPreIncrement : CmdUnary
  METHODS
    method op->String
      return "++"
endClass

class CmdPreDecrement : CmdUnary
  METHODS
    method op->String
      return "--"
endClass

class CmdPostIncrement : CmdPostUnary
  METHODS
    method op->String
      return "++"
endClass

class CmdPostDecrement : CmdPostUnary
  METHODS
    method op->String
      return "--"
endClass

class CmdAccess( t, value:String ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 0 )
      value = t.content

    method to->String
      return value
endClass

class CmdLiteralInt32( t, value:Int32 ) : Cmd
  METHODS
    method init( t, args:CmdInitArgs )
      args.require_count( 0 )
      value = t.content->Int32

    method to->String
      return value->String
endClass
