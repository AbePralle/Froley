[settings]
id_start    alpha '_'
id_continue alpha numeric '_'
language    JSON

[whitespace]
SPACE ' '
TAB   '\t'
EOL   '\n'
CR    '\r'

[tokens]
STRING   string  [content]
NUMBER   number  [content]

[keywords]
FALSE         false
NULL          null
TRUE          true

[symbols]
CLOSE_BRACKET ]
CLOSE_CURLY   }
COLON         :
COMMA         ,
OPEN_BRACKET  [
OPEN_CURLY    {

[tokenizer]
<<tokenize>>
which (input)
  discardAny [whitespace]
endWhich

markSourcePosition
if (not hasAnother) halt

if (scanIdentifier)
  which (buffer)
    produceAny [keywords]
    others
      syntaxError "Syntax error - unrecognized identifier."
  endWhich
endIf

which (input)
  produceAny [symbols]
  case '"'
    scan_string
  others
    ch = peek
    if (ch is digit or ch == '-')
      scan_number
    endIf
endWhich

syntaxError

<scan_number>
clear buffer

# Optional negative sign
if (consume('-')) collect '-'

# Whole portion of number
if (consume('0'))
  collect '0'
else
  collect_digits
endIf

# Optional fractional part
if (consume('.'))
  collect '.'
  collect_digits
endIf

# Optional exponent
if (consume('e') or consume('E'))
  collect 'E'
  ch = peek
  if (consume('+') or consume('-')) collect ch
  collect_digits
endIf

produce NUMBER

<collect_digits>
ch = peek
if (ch is not digit) syntaxError "Digit expected."
while (ch is digit)
  ch = read
  collect ch
  ch = peek
endWhile
return

<scan_string>
clear buffer
ch = peek
while (hasAnother and ch!='"' and ch>=' ' and ch != 127)
  ch = read
  if (ch == '\\')
    which (input)
      case '"':  collect '"'
      case '\\': collect '\\'
      case '/':  collect '/'
      case 'b':  collect 8
      case 'f':  collect 12
      case 'n':  collect '\n'
      case 'r':  collect '\r'
      case 't':  collect '\t'
      case 'u':  ch = scanDigits 4 base 16; collect ch
      others
        clear buffer
        collect "Syntax error - unrecognized escape sequence '\\"
        ch = peek
        collect ch
        collect "'."
        syntaxError buffer
    endWhich
  else
    collect ch
  endIf
  ch = peek
endWhile
if (not consume('"')) syntaxError "Closing \" expected."
produce STRING

[parser]
- value
  on '{', definitions, '}' -> CmdObject(definitions:CmdList)
  on '[', array, ']'       -> CmdArray(array:CmdList)
  on STRING                -> CmdString( value=t.content:String )
  on NUMBER                -> CmdNumber( value=t.content:Real64 )
  on "true"                -> CmdTrue
  on "false"               -> CmdFalse
  on "null"                -> CmdNull
  syntaxError

- definitions
  beginList
    if (not nextIs('}'))
      definition
      while (consume(','))
        definition
      endWhile
    endIf
  createList

- definition
  on STRING, ':', value -> CmdMapping(name=t.content:String,value)
  syntaxError

- array
  beginList
    if (not nextIs(']'))
      value
      while (consume(','))
        value
      endWhile
    endIf
  createList

