module Froley

Froley.code_generators//json = GenerateJSON()

class GenerateJSON : CodeGenerator
  PROPERTIES
    source_timestamp : Real64

  METHODS
    method add_option( name:String, value:String )
      which (name)
        case "output"
          # Override 'output' to avoid putting the separator on the end
          if (value == "") throw FroleyError( "Output folder or filepath expected (--output=<folder>)." )
          options//output = value
        others
          prior.add_option( name, value )
      endWhich

    method process( info:Value )
      local output = select{options//output||""}
      if (output == "" or File.is_folder(output))
        local filename = info//language + ".json"
        if (output == "") output = filename
        else output = File.ensure_ends_with_separator(output) + filename
      endIf

      if (File.is_newer_than(info//source_filepath,output))
        println "Writing " + output
        File.save( output, info.to_json )
      endIf
endClass
